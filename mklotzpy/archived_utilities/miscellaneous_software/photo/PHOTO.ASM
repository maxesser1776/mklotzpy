;				PHOTO.ASM

;APPENDS A COPY OF THE SCREEN TO THE FILE C:PHOTO

MAIN	SEGMENT	PARA
	ORG	100H
	ASSUME CS:MAIN,DS:MAIN

CR	EQU	13
LF	EQU	10
EOF	EQU	1AH

BEGIN:	JMP	SHORT START

SCREEN_SEG	DW	0B800H
BIOS_SEG	DW	40H

PFILE	DB	'C:PHOTO',0	;PHOTO FILESPEC
PHAND	DW	?		;PHOTO FILEHANDLE
PLINE	DB	80 DUP (?),CR,LF
EOFM	DB	EOF		;END-OF-FILE MESSAGE

START:
	XOR	AX,AX
	MOV	ES,AX
	MOV	AL,ES:[0449H]		;CURRENT VIDEO MODE
	CMP	AL,7			;CHECK IF MONOCHROME
	JNE	PH100
	MOV	SCREEN_SEG,0B000H

PH100:	CALL	POPEN			;OPEN PHOTO FILE
	JNC	PH300			;EXIT ON FILE OPEN FAILURE
	JMP	PH999
PH300:	MOV	AH,15
	INT	10H
	CMP	AL,3			;EXIT IF NOT TEXT MODE
	JBE	PH310
	JMP	PH900
PH310:	MOV	AL,BH			;SET UP PAGE ADDRESS
	XOR	AH,AH
	MOV	BX,1000H
	MUL	BX
	MOV	SI,AX

	MOV	ES,BIOS_SEG
	MOV	AL,ES:[84H]		;ROWS - 1
	INC	AL
	XOR	AH,AH
	MOV	BP,AX			;COUNTS LINES

	MOV	DS,SCREEN_SEG
	PUSH	SI			;ELIMINATE BLANK LINES
	MOV	CX,BP
	MOV	DX,0
PH400:	INC	DL
	PUSH	CX
	PUSH	SI
	MOV	CX,80
PH410:	CMP	BYTE PTR DS:[SI],' '
	JE	PH420
	MOV	DH,DL
	JMP	SHORT PH460
PH420:	INC	SI
	INC	SI
	LOOP	PH410
	JMP	SHORT PH460
PH460:	POP	SI
	ADD	SI,160
	POP	CX
	LOOP	PH400
	MOV	DL,DH
	XOR	DH,DH
	MOV	BP,DX
	POP	SI

PH500:	PUSH	SI			;DON'T COPY TRAILING BLANKS ON LINE
	PUSH	SI
	MOV	CX,80
	MOV	BX,0
PH510:	CMP	BYTE PTR DS:[SI],' '
	JE	PH520
	MOV	BH,BL
PH520:	INC	BL
	INC	SI
	INC	SI
	LOOP	PH510
	XCHG	BH,BL
	XOR	BH,BH
	MOV	CX,BX
	INC	CX
	
	POP	SI
	PUSH	CS
	POP	ES
	PUSH	CX
	MOV	DI,OFFSET PLINE
PH550:	LODSB
	INC	SI
	STOSB
	LOOP	PH550
	MOV	AX,0A0DH		;TERMINATE LINE
	STOSW
	POP	CX
	ADD	CX,2
	
	PUSH	DS
	PUSH	CS
	POP	DS
	MOV	DX,OFFSET PLINE
	MOV	AH,40H			;WRITE LINE TO FILE
	MOV	BX,PHAND
	INT	21H
	POP	DS
	POP	SI
	JC	PH900
	ADD	SI,160
	DEC	BP
	JNZ	PH500

PH900:	CALL	PCLOSE			;CLOSE FILE

PH999:	INT	20H

;-----------------------------------------------------------------------------

;OPEN AND SETUP THE PHOTO FILE, WRITE TO IT, AND CLOSE IT
;EXITS WITH NO MESSAGE AND CF SET IF FAILURES OCCUR

POPEN:	MOV	AX,3D01H		;ATTEMPT TO OPEN FILE - WRITE ONLY
	MOV	DX,OFFSET PFILE
	INT	21H
	MOV	PHAND,AX
	JNC	PO300
	MOV	AH,3CH			;IF FILE DIDN'T EXIST, CREATE IT
	XOR	CX,CX
	MOV	DX,OFFSET PFILE
	INT	21H
	MOV	PHAND,AX
	JC	PO999			;EXIT ON FILE CREATION FAILURE
	RET

PO300:	MOV	AX,4202H		;MOVE POINTER TO END OF FILE
	MOV	BX,PHAND
	MOV	CX,-1			;BACK OVER EOF MARKER
	MOV	DX,CX
	INT	21H
	JC	PO999			;EXIT ON FAILURE
PO998:	CLC
	RET
PO999:	STC
	RET

PCLOSE:
	LEA	DX,EOFM			;WRITE EOF MARKER
	MOV	CX,1
	MOV	AH,40H			;WRITE LINE TO FILE
	MOV	BX,PHAND
	INT	21H
	MOV	AH,3EH			;CLOSE FILE
	MOV	BX,PHAND
	INT	21H
	RET

MAIN	ENDS
	END	BEGIN
